# Maintainer: Johannes Brakensiek <letterus AT gnustep DOT app>

_pkgname=DictionaryReader
pkgname=dictionaryreader.app-ngr
pkgver=0.1.0
pkgrel=1
pkgdesc='A dictionary reader for dict protocol online and locally.'
arch=(i686 x86_64 arm armv6h armv7h aarch64)
url='https://github.com/Letterus/DictionaryReader'
license=('MIT')
groups=('gnustep-ngr-apps')
depends=('gnustep-ngr-base'
        'gnustep-ngr-gui'
        'gnustep-ngr-back')
makedepends=('gnustep-ngr-make' 'clang')
options=(debug strip)
provides=('dictionaryreader.app')
install="dictionaryreader.install"
source=("${pkgname}-${pkgver}.tar.xz")
sha512sums=('SKIP')

build() {
  cd ${pkgname}-${pkgver}
  source /usr/GNUstep/System/Library/Makefiles/GNUstep.sh
  make debug=yes
}

package() {
  cd ${pkgname}-${pkgver}
  source /usr/GNUstep/System/Library/Makefiles/GNUstep.sh
  make DESTDIR="$pkgdir" GNUSTEP_INSTALLATION_DOMAIN=SYSTEM install
  sed -i 's/Local/System/g' "$pkgdir/usr/GNUstep/System/Applications/$_pkgname.app/Resources/$_pkgname.desktop"
  install -Dm644 "$pkgdir/usr/GNUstep/System/Applications/$_pkgname.app/Resources/$_pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"
}
