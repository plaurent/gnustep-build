# Maintainer: Johannes Brakensiek <letterus AT gnustep DOT app>

pkgname=gnustep-ngr-gorm
_pkgname='Gorm'
pkgver=1.2.26
pkgrel=1
pkgdesc="GNUstep's easy-to-use interface designer"
arch=(i686 x86_64 arm armv6h armv7h aarch64)
url="http://www.gnustep.org/experience/Gorm.html"
license=('GPL')
depends=('gnustep-ngr-base' 'gnustep-ngr-gui' 'gnustep-ngr-back')
makedepends=('gnustep-ngr-make' clang)
options=(debug strip)
groups=('gnustep-ngr-dev')
provides=('gorm.app')
conflicts=('gorm.app' 'gnustep-ngr-gorm-git')
install="gorm.install"
source=("${pkgname}-${pkgver}.tar.xz")
sha256sums=('SKIP')

build() {
  cd ${pkgname}-${pkgver}
  source /usr/GNUstep/System/Library/Makefiles/GNUstep.sh
  make debug=yes
}

package() {
  cd ${pkgname}-${pkgver}
  source /usr/GNUstep/System/Library/Makefiles/GNUstep.sh
  make DESTDIR="$pkgdir" GNUSTEP_INSTALLATION_DOMAIN=SYSTEM install
  sed -i 's/Local/System/g' "$pkgdir/usr/GNUstep/System/Applications/$_pkgname.app/Resources/$_pkgname.desktop"
  install -Dm644 "$pkgdir/usr/GNUstep/System/Applications/$_pkgname.app/Resources/$_pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"
}
