# Maintainer: Johannes Brakensiek <letterus AT gnustep DOT app>

_pkgname=GNUMail
pkgname=gnumail.app-ngr
pkgver=1.3.0
pkgrel=1
pkgdesc='A mailclient built using GNUstep and a clone of NeXT Mail.app'
arch=(i686 x86_64 arm armv6h armv7h aarch64)
url='http://wiki.gnustep.org/index.php/GNUMail'
license=('LGPL2')
groups=('gnustep-ngr-apps')
depends=('gnustep-ngr-base'
        'gnustep-ngr-gui'
        'gnustep-ngr-back'
        'pantomime.framework-ngr'
        'addresses.app-ngr')
makedepends=('gnustep-ngr-make' 'clang')
options=(debug strip)
provides=('gnumail.app')
conflicts=('gnumail.app' 'gnumail.app-ngr-svn')
install="gnumail.install"
source=("${pkgname}-${pkgver}.tar.xz")
sha256sums=('SKIP')

build() {
  cd ${pkgname}-${pkgver}
  source /usr/GNUstep/System/Library/Makefiles/GNUstep.sh
  make debug=yes
}

package() {
  cd ${pkgname}-${pkgver}
  source /usr/GNUstep/System/Library/Makefiles/GNUstep.sh
  make DESTDIR="$pkgdir" GNUSTEP_INSTALLATION_DOMAIN=SYSTEM install
  sed -i 's/Local/System/g' "$pkgdir/usr/GNUstep/System/Applications/$_pkgname.app/Resources/$_pkgname.desktop"
  install -Dm644 "$pkgdir/usr/GNUstep/System/Applications/$_pkgname.app/Resources/$_pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"
}
